users.routes : '{Route, Exception, UserStorage, Log} ()
users.routes =
  use Parser /
  use Route <|>
  use Text ++
  use User decoder toJson
  health = do
    noCapture GET (s "health")
    ok.text "ok"
  getAllUsers = do
    noCapture GET (s "users")
    info "Getting all users" []
    userList = UserStorage.getAllUsers()
    json = Json.array (List.map toJson userList)
    ok.json json
  getUserById = do
    odUserId = route GET (s "users" / Parser.text)
    info "Getting user" [("id", odUserId)]
    match getUser (UserId odUserId) with
      Some user -> ok.json (toJson user)
      None      -> notFound.text ("User not found: " ++ odUserId)
  createUser = do
    noCapture POST (s "users")
    user = decodeJson decoder
    info "Creating user" [("id", User.id user)]
    upsertUser user
    setStatus (Status 201 "Created")
    ok.json (toJson user)
  updateUser = do
    odUserId = route PUT (s "users" / Parser.text)
    user = decodeJson decoder
    info "Updating user" [("id", odUserId)]
    match getUser (UserId odUserId) with
      Some _ ->
        updatedUser = User odUserId (User.name user) (User.email user) (User.avatarUrl user) (User.createdAt user) (profile user) (selectedStores user) (shoppingListIds user)
        upsertUser updatedUser
        ok.json (toJson updatedUser)
      None   -> notFound.text ("User not found: " ++ odUserId)
  deleteUser = do
    odUserId = route DELETE (s "users" / Parser.text)
    info "Deleting user" [("id", odUserId)]
    match getUser (UserId odUserId) with
      Some _ ->
        UserStorage.deleteUser (UserId odUserId)
        ok.text "Deleted"
      None   -> notFound.text ("User not found: " ++ odUserId)
  health <|> getUserById <|> getAllUsers <|> createUser <|> updateUser <|> deleteUser